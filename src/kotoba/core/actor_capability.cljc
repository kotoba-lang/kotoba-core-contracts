(ns kotoba.core.actor-capability
  "Shared, host-neutral contract for bounded actor capability envelopes.

  Tamaki may plan and emit an envelope; Kototama, Fleet, or another tender
  independently validates it. This namespace is the vocabulary authority.
  It does not create HostCaps or perform an effect."
  (:require [clojure.set :as set]
            [clojure.string :as str]))

(def contract
  {:schema "kotoba.actor.capability-contract.v1"
   :authority "kotoba-lang/kotoba-core-contracts"
   :envelope-version 1
   :substrates #{:kototama-wasm}
   :execution-roles #{:control-guest :worker-guest}
   :decisions #{:autonomous :approval-required :voice-required :blocked}
   :abi {:namespace "actor:host" :version 0}
   :import-effects
   {:gen-keypair #{:crypto :secret}
    :sign #{:crypto :secret}
    :verify #{:crypto}
    :sha256-hex #{:crypto}
    :http-post #{:network-write}
    :http-post-headers #{:network-write}
    :http-fetch #{:network-read}
    :log-read #{:storage-read}
    :log-write #{:storage-write}
    :clock-monotonic #{:clock}
    :llm-infer #{:llm-inference}
    :cbor-encode #{:codec}
    :json-encode #{:codec}
    :json-extract-field #{:codec}}
   ;; Business responsibility is not authority. This explicit realization
   ;; table is the only bridge from a responsibility to actor:host imports.
   :capability-imports
   {:organism/heartbeat #{:clock-monotonic :sha256-hex :log-write}
    :telemetry/read #{:log-read}
    :telemetry/write #{:log-write}
    :network/fetch #{:http-fetch}
    :network/post #{:http-post}
    :llm/infer #{:llm-infer}
    :identity/generate #{:gen-keypair}
    :identity/sign #{:sign}
    :identity/verify #{:verify}
    :content/digest #{:sha256-hex}
    :codec/cbor #{:cbor-encode}
    :codec/json #{:json-encode :json-extract-field}}})

(def envelope-version (:envelope-version contract))
(def actor-host-namespace (get-in contract [:abi :namespace]))
(def actor-host-version (get-in contract [:abi :version]))
(def substrates (:substrates contract))
(def execution-roles (:execution-roles contract))
(def decisions (:decisions contract))
(def import-effects (:import-effects contract))
(def capability-imports (:capability-imports contract))
(def known-imports (set (keys import-effects)))

(defn required-imports [capabilities]
  (reduce set/union #{} (map capability-imports capabilities)))

(defn effects-for [imports]
  (reduce set/union #{} (map import-effects imports)))

(defn- error [kind data]
  (assoc data :error kind))

(defn validate-execution
  "Validate an actor-owned execution declaration before envelope emission."
  [actor-capabilities execution]
  (let [substrate (:execution/substrate execution)
        role (:execution/role execution)
        realizes (set (:execution/realizes execution))
        declaration (:execution/capability-contract execution)
        imports (set (:imports declaration))
        grants (set (:grants declaration))
        limits (:limits declaration)
        policies (:effect-policy declaration)
        unknown-imports (set/difference imports known-imports)
        unknown-grants (set/difference grants known-imports)
        unknown-capabilities (set/difference realizes
                                            (set (keys capability-imports)))
        undeclared-capabilities (set/difference realizes
                                                (set actor-capabilities))
        required (required-imports realizes)
        missing-imports (set/difference required imports)
        missing-grants (set/difference imports grants)
        effects (effects-for imports)
        missing-policies (set/difference effects (set (keys policies)))
        invalid-policies (into {}
                               (remove (fn [[_ decision]]
                                         (contains? decisions decision)))
                               policies)
        network? (seq (set/intersection effects
                                        #{:network-read :network-write}))
        prefixes (:allowed-url-prefixes limits)
        secret? (contains? effects :secret)
        write? (contains? effects :storage-write)
        errors
        (cond-> []
          (not (contains? substrates substrate))
          (conj (error :execution/substrate
                       {:expected substrates :actual substrate}))

          (not (contains? execution-roles role))
          (conj (error :execution/role {:actual role}))

          (not= envelope-version (:contract/version declaration))
          (conj (error :contract/version
                       {:expected envelope-version
                        :actual (:contract/version declaration)}))

          (not= actor-host-namespace (:abi/namespace declaration))
          (conj (error :abi/namespace
                       {:expected actor-host-namespace
                        :actual (:abi/namespace declaration)}))

          (not= actor-host-version (:abi/version declaration))
          (conj (error :abi/version
                       {:expected actor-host-version
                        :actual (:abi/version declaration)}))

          (seq unknown-capabilities)
          (conj (error :capabilities/unrealizable
                       {:capabilities unknown-capabilities}))

          (seq undeclared-capabilities)
          (conj (error :capabilities/not-owned
                       {:capabilities undeclared-capabilities}))

          (seq unknown-imports)
          (conj (error :imports/unknown {:imports unknown-imports}))

          (seq unknown-grants)
          (conj (error :grants/unknown {:grants unknown-grants}))

          (seq missing-imports)
          (conj (error :imports/missing-for-capability
                       {:imports missing-imports}))

          (seq missing-grants)
          (conj (error :grants/missing {:imports missing-grants}))

          (seq missing-policies)
          (conj (error :effect-policy/missing
                       {:effects missing-policies}))

          (seq invalid-policies)
          (conj (error :effect-policy/invalid
                       {:policies invalid-policies}))

          (and network? (or (nil? prefixes) (empty? prefixes)
                            (not-every? #(and (string? %)
                                              (not (str/blank? %)))
                                        prefixes)))
          (conj (error :network/allowlist-required {}))

          (and secret? (not (true? (:allow-secret-imports? limits))))
          (conj (error :limits/secret-imports {}))

          (and write? (not (true? (:allow-write-imports? limits))))
          (conj (error :limits/write-imports {}))

          (and (contains? effects :network-write)
               (= :autonomous (:network-write policies)))
          (conj (error :effect-policy/network-write-needs-human {}))

          (and secret? (= :autonomous (:secret policies)))
          (conj (error :effect-policy/secret-needs-human {}))

          (and (contains? imports :http-fetch)
               (not (pos-int? (:max-http-fetches limits))))
          (conj (error :limits/http-fetches {}))

          (and (seq (set/intersection imports
                                      #{:http-post :http-post-headers}))
               (not (pos-int? (:max-http-posts limits))))
          (conj (error :limits/http-posts {}))

          (and (contains? imports :llm-infer)
               (not (pos-int? (:max-llm-infers limits))))
          (conj (error :limits/llm-infers {})))]
    {:ok? (empty? errors)
     :contract/version envelope-version
     :execution/substrate substrate
     :execution/role role
     :realizes realizes
     :required-imports required
     :requested-imports imports
     :grants grants
     :effects effects
     :limits limits
     :effect-policy policies
     :errors errors}))

(defn validate-execution!
  [actor-capabilities execution]
  (let [report (validate-execution actor-capabilities execution)]
    (when-not (:ok? report)
      (throw (ex-info "Actor capability execution rejected" report)))
    report))

(defn execution-envelope
  "Emit the minimal authority payload; private ActorSpec data is absent."
  [actor-id actor-capabilities execution]
  (let [report (validate-execution! actor-capabilities execution)]
    {:tamaki.capability/version envelope-version
     :tamaki.capability/actor (str actor-id)
     :tamaki.capability/substrate (:execution/substrate report)
     :tamaki.capability/role (:execution/role report)
     :tamaki.capability/abi
     {:namespace actor-host-namespace :version actor-host-version}
     :tamaki.capability/imports (:requested-imports report)
     :tamaki.capability/grants (:grants report)
     :tamaki.capability/limits (:limits report)
     :tamaki.capability/effect-policy (:effect-policy report)}))

(defn validate-envelope
  "Host-neutral, fail-closed validation used again at every tender boundary."
  [envelope]
  (let [imports (set (:tamaki.capability/imports envelope))
        grants (set (:tamaki.capability/grants envelope))
        limits (:tamaki.capability/limits envelope)
        policies (:tamaki.capability/effect-policy envelope)
        abi (:tamaki.capability/abi envelope)
        unknown-imports (set/difference imports known-imports)
        unknown-grants (set/difference grants known-imports)
        missing-grants (set/difference imports grants)
        excess-grants (set/difference grants imports)
        effects (effects-for imports)
        missing-policies (set/difference effects (set (keys policies)))
        invalid-policies (into {}
                               (remove (fn [[_ decision]]
                                         (contains? decisions decision)))
                               policies)
        prefixes (:allowed-url-prefixes limits)
        errors
        (cond-> []
          (not= envelope-version (:tamaki.capability/version envelope))
          (conj (error :tamaki.capability/version
                       {:expected envelope-version
                        :actual (:tamaki.capability/version envelope)}))
          (str/blank? (:tamaki.capability/actor envelope))
          (conj (error :tamaki.capability/actor {}))
          (not (contains? substrates (:tamaki.capability/substrate envelope)))
          (conj (error :tamaki.capability/substrate {}))
          (not (contains? execution-roles (:tamaki.capability/role envelope)))
          (conj (error :tamaki.capability/role {}))
          (not= (:abi contract) abi)
          (conj (error :tamaki.capability/abi
                       {:expected (:abi contract) :actual abi}))
          (seq unknown-imports)
          (conj (error :imports/unknown {:imports unknown-imports}))
          (seq unknown-grants)
          (conj (error :grants/unknown {:grants unknown-grants}))
          (seq missing-grants)
          (conj (error :grants/missing {:imports missing-grants}))
          (seq excess-grants)
          (conj (error :grants/excess {:grants excess-grants}))
          (seq missing-policies)
          (conj (error :effect-policy/missing {:effects missing-policies}))
          (seq invalid-policies)
          (conj (error :effect-policy/invalid {:policies invalid-policies}))
          (and (seq (set/intersection effects #{:network-read :network-write}))
               (or (nil? prefixes) (empty? prefixes)))
          (conj (error :network/allowlist-required {}))
          (and (contains? effects :network-write)
               (= :autonomous (:network-write policies)))
          (conj (error :effect-policy/network-write-needs-human {}))
          (and (contains? effects :secret)
               (= :autonomous (:secret policies)))
          (conj (error :effect-policy/secret-needs-human {}))
          (and (contains? effects :secret)
               (not (true? (:allow-secret-imports? limits))))
          (conj (error :limits/secret-imports {}))
          (and (contains? effects :storage-write)
               (not (true? (:allow-write-imports? limits))))
          (conj (error :limits/write-imports {}))
          (and (contains? imports :http-fetch)
               (not (pos-int? (:max-http-fetches limits))))
          (conj (error :limits/http-fetches {}))
          (and (seq (set/intersection imports
                                      #{:http-post :http-post-headers}))
               (not (pos-int? (:max-http-posts limits))))
          (conj (error :limits/http-posts {}))
          (and (contains? imports :llm-infer)
               (not (pos-int? (:max-llm-infers limits))))
          (conj (error :limits/llm-infers {})))]
    {:ok? (empty? errors)
     :actor (:tamaki.capability/actor envelope)
     :imports imports
     :grants grants
     :effects effects
     :limits limits
     :effect-policy policies
     :errors errors}))

(defn validate-envelope!
  [envelope]
  (let [report (validate-envelope envelope)]
    (when-not (:ok? report)
      (throw (ex-info "Actor capability envelope rejected" report)))
    report))
