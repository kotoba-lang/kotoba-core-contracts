(ns scaffold-capability-repos
  "Generate one importable repository skeleton per actor:host capability.

  Usage:
    clojure -M -m scaffold-capability-repos /absolute/output/root
    clojure -M -m scaffold-capability-repos /absolute/output/root --update"
  (:require [clojure.java.io :as io]
            [clojure.string :as str]
            [kotoba.core.capability-repository :as repository]
            [kotoba.core.contracts :as contracts]))

(defn- core-contracts-sha []
  (let [sha (System/getenv "CAPABILITY_CORE_CONTRACTS_SHA")]
    (when-not (and sha (re-matches #"[0-9a-f]{40}" sha))
      (throw (ex-info
              "CAPABILITY_CORE_CONTRACTS_SHA must pin the published contract"
              {:value sha})))
    sha))

(defn- ns-path [capability-id]
  (str/replace capability-id #"-" "_"))

(defn- manifest-source [manifest]
  (str "(ns " (repository/namespace-symbol (:capability/id manifest)) "\n"
       "  \"Importable contract for " (:capability/id manifest) ".\")\n\n"
       "(def manifest\n"
       "  " (pr-str manifest) ")\n"))

(defn- test-source [manifest]
  (let [ns-name (repository/namespace-symbol (:capability/id manifest))]
    (str "(ns " ns-name "-test\n"
         "  (:require [clojure.test :refer [deftest is]]\n"
         "            [" ns-name " :as capability]\n"
         "            [kotoba.core.capability-repository :as repository]\n"
         "            [kotoba.core.contracts :as contracts]))\n\n"
         "(deftest manifest-conforms\n"
         "  (is (= [] (repository/validate-manifest\n"
         "             (contracts/capability-contract)\n"
         "             capability/manifest))))\n")))

(defn- deps-source [sha]
  (pr-str
   {:paths ["src"]
    :deps
    {'io.github.kotoba-lang/kotoba-core-contracts
     {:git/url "https://github.com/kotoba-lang/kotoba-core-contracts.git"
      :git/sha sha}}
    :aliases
    {:test
     {:extra-paths ["test"]
      :extra-deps
      {'io.github.cognitect-labs/test-runner
       {:git/tag "v0.5.1" :git/sha "dfb30dd"}}
      :main-opts ["-m" "cognitect.test-runner"]}}}))

(defn- readme-source [manifest]
  (str "# " (repository/repository-name (:capability/id manifest)) "\n\n"
       "Atomic authority package for `" (:capability/id manifest) "`.\n\n"
       "- imports: `" (pr-str (:capability/imports manifest)) "`\n"
       "- effects: `" (pr-str (:capability/effects manifest)) "`\n"
       "- default policy: `" (:capability/default-policy manifest) "`\n"
       "- semantic definition CID: `" (:capability/definition-cid manifest) "`\n"
       "- hash contract CID: `" (:capability/hash-contract-cid manifest) "`\n"
       "- provider status: `contract-only`\n\n"
       "The repository name is a discovery alias. The semantic definition CID\n"
       "is the immutable import identity. Importing it does not grant runtime\n"
       "authority: Tamaki must request it explicitly and Kototama must admit\n"
       "the sealed envelope.\n\n"
       "```sh\nclojure -M:test\n```\n"))

(defn repo-files [manifest sha]
  (let [path (ns-path (:capability/id manifest))]
    {"README.md" (readme-source manifest)
     ".gitignore" ".cpcache/\n.clj-kondo/.cache/\ntarget/\n"
     "deps.edn" (deps-source sha)
     "capability.edn" (pr-str manifest)
     (str "src/kotoba/capability/" path ".cljc")
     (manifest-source manifest)
     (str "test/kotoba/capability/" path "_test.clj")
     (test-source manifest)}))

(defn write-repo!
  ([root manifest sha] (write-repo! root manifest sha false))
  ([root manifest sha update?]
  (let [dir (io/file root (repository/repository-name
                           (:capability/id manifest)))]
    (when (and (.exists dir) (not update?))
      (throw (ex-info "refusing to overwrite capability repository"
                      {:path (.getCanonicalPath dir)})))
    (doseq [[path content] (repo-files manifest sha)]
      (let [file (io/file dir path)]
        (.mkdirs (.getParentFile file))
        (spit file content)))
    (.getCanonicalPath dir))))

(defn -main [& [root mode]]
  (when-not root
    (throw (ex-info "absolute output root required" {})))
  (when-not (.isAbsolute (io/file root))
    (throw (ex-info "output root must be absolute" {:root root})))
  (when-not (contains? #{nil "--update"} mode)
    (throw (ex-info "unknown scaffold mode" {:mode mode})))
  (let [update? (= "--update" mode)
        sha (core-contracts-sha)
        runtime-contract (contracts/capability-contract)]
    (doseq [manifest (repository/full-catalog runtime-contract)]
      (let [dir (io/file root (repository/repository-name
                               (:capability/id manifest)))]
        (if (and (.exists dir) (not update?))
          (println "SKIP" (.getCanonicalPath dir))
          (println (if update? "UPDATE" "CREATE")
                   (write-repo! root manifest sha update?)))))))
